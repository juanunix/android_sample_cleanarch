apply plugin: 'com.android.application'

// Generates a build date stamp.
def getDateStamp() {
    def date = new Date()
    def formattedDate = date.format('ddMMHHmmss')
    return formattedDate
}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.0"
    defaultConfig {
        applicationId "com.felipeporge.sample"
        minSdkVersion 17
        targetSdkVersion 25
        versionCode 1
        versionName "1.0.0 build " + getDateStamp()
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        // Generates the version string.
        applicationVariants.all { variant ->
            variant.resValue "string", "gradle_versionName", "v" + variant.versionName
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

// Gradle constants.
ext {
    // FOR TESTS:
    JUNIT_VERSION = '4.12'
    ESPRESSO_VERSION = '2.2.2'

    // DEPENDENCY INJECTION:
    DAGGER_VERSION ='2.0'
    BUTTER_KNIFE_VERSION = '8.4.0'

    // FOR GENERAL:
    ANDROID_SUPPORT_VERSION = '25.0.0'
}

dependencies {
    // Jar Libs:
    compile fileTree(include: ['*.jar'], dir: 'libs')

    // ** Other Layers ** :
    compile project(':domain')
    compile project(':data')

    // Android Support:
    compile "com.android.support:appcompat-v7:$ANDROID_SUPPORT_VERSION"
    compile "com.android.support:design:$ANDROID_SUPPORT_VERSION"
    compile "com.android.support:recyclerview-v7:$ANDROID_SUPPORT_VERSION"

    // Tests:
    testCompile "junit:junit:$JUNIT_VERSION"
    androidTestCompile("com.android.support.test.espresso:espresso-core:$ESPRESSO_VERSION", {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    // ButterKnife: Views injection.
    compile "com.jakewharton:butterknife:$BUTTER_KNIFE_VERSION"
    annotationProcessor "com.jakewharton:butterknife-compiler:$BUTTER_KNIFE_VERSION"

    // Circle Indicators
    compile 'me.relex:circleindicator:1.1.8@aar'

    // Time utils.
    compile 'org.apache.commons:commons-lang3:3.0'

    // Dagger 2:
    compile "com.google.dagger:dagger:$DAGGER_VERSION"
    annotationProcessor "com.google.dagger:dagger-compiler:$DAGGER_VERSION"
    provided 'org.glassfish:javax.annotation:10.0-b28'

    // Fresco by Facebook
    compile 'com.facebook.fresco:fresco:0.14.1'
}